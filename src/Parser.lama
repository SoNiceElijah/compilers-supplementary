-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop (m) {
  [s (m), fun (l, o, r) { Binop (o, l, r) }]
}

var elifChain = memo $ eta syntax (
    kElif cond=exp kThen tru=stmt fls=elifChain { If (cond, tru, fls) }
  | kElse fls=stmt { fls }
);

-- Primary expression
var primary = memo $ eta syntax (x=decimal {Const (stringInt (x))} |
                                 x=lident  {Var (x)}               |
                                 inbr[s("("), exp, s(")")]),
      exp = memo $ eta (expr ({
          [ Left, map (binop, { "!!" }) ]
        , [ Left, map (binop, { "&&" }) ]
        , [ Nona, map (binop, { "<", ">", "<=", ">=", "!=", "==" }) ]
        , [ Left, map (binop, { "+", "-" }) ]
        , [ Left, map (binop, { "*", "/", "%" }) ]
      }, primary));

var stmt = memo $ eta expr (
  {[ Left, {[ s (";"), fun (l, o, r) { Seq (l, r) } ]} ]},
  syntax (
      kSkip { Skip }
    | x=lident s [":="] e=exp { Assn (x, e) }
    | kRead s ["("] x=lident s [")"] { Read (x) }
    | kWrite s ["("] e=exp s [")"] { Write (e) }
    | kIf cond=exp kThen tru=stmt kFi { If (cond, tru, Skip) } 
    | kIf cond=exp kThen tru=stmt fls=elifChain kFi { If (cond, tru, fls) } 
    | kWhile cond=exp kDo body=stmt kOd { While (cond, body) }
    | kDo body=stmt kWhile cond=exp kOd { DoWhile (body, cond) } 
    | kFor start=stmt s [","] cond=exp s [","] post=stmt kDo body=stmt kOd { Seq (start, While (cond, Seq (body, post))) }
  )
);   

-- Public top-level parser
public parse = stmt;
